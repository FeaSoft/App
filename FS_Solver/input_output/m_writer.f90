! Description:
! Output database file writer.
module m_writer
    use data_model
    implicit none
    
    private
    public write_output
    
    character(*), parameter :: comment     = '# ',                                                                               &
                               separator   = '# ==============================================================================', &
                               indentation = '    '
    
    contains
    
    ! Description:
    ! Writes the specified output database to file.
    subroutine write_output(file, mesh, odb)
        character(*), intent(in) :: file ! output database file path
        type(t_mesh), intent(in) :: mesh ! finite element mesh
        type(t_odb),  intent(in) :: odb  ! output database
        
        ! writer helper variables
        character(8)  :: date ! current date
        character(10) :: time ! current time
        integer       :: i, j ! loop counters
        
        ! open output database file for writing
        open(unit=1, action='write', file=file)
        
        ! header
        call date_and_time(date, time)
        write(unit=1, fmt='(A)'), separator
        write(unit=1, fmt='(A,A)'), comment, 'OUTPUT DATABASE GENERATED BY FEASOFT'
        write(unit=1, fmt='(A,A,"-",A,"-",A," ",A,":",A,":",A)'), comment, date(1:4), date(5:6), date(7:8), time(1:2), time(3:4), time(5:6)
        write(unit=1, fmt='(A)'), separator
        write(unit=1, fmt='(A)'), 'from dataModel import *'
        write(unit=1, fmt='(A)'), ''
        
        ! nodes
        write(unit=1, fmt='(A)'), separator
        write(unit=1, fmt='(A,A)'), comment, 'NODAL COORDINATES'
        write(unit=1, fmt='(A)'), separator
        write(unit=1, fmt='(A)'), 'nodeData = ('
        do i = 1, mesh%n_nodes
            write(unit=1, fmt='(A,"(",SP,E15.8,", ",E15.8,", ",E15.8,"),")'), indentation, mesh%nodes(i)%coordinates
        end do
        write(unit=1, fmt='(A)'), ')'
        write(unit=1, fmt='(A)'), ''
        
        ! elements
        write(unit=1, fmt='(A)'), separator
        write(unit=1, fmt='(A,A)'), comment, 'ELEMENT CONNECTIVITY'
        write(unit=1, fmt='(A)'), separator
        write(unit=1, fmt='(A)'), 'elementData = ('
        do i = 1, mesh%n_elements
            write(unit=1, fmt='(A,"(''",A,"'', (")', advance='no'), indentation, trim(mesh%elements(i)%t_name)
            do j = 1, mesh%elements(i)%n_nodes
                write(unit=1, fmt='(I0)', advance='no'), mesh%elements(i)%i_nodes(j) - 1 ! 1- to 0-based indexing
                if (j < mesh%elements(i)%n_nodes) then
                    write(unit=1, fmt='(A)', advance='no'), ', '
                else
                    write(unit=1, fmt='(A)', advance='yes'), ')),'
                end if
            end do
        end do
        write(unit=1, fmt='(A)'), ')'
        write(unit=1, fmt='(A)'), ''
        
        ! mesh
        write(unit=1, fmt='(A)'), separator
        write(unit=1, fmt='(A,A)'), comment, 'FINITE ELEMENT MESH'
        write(unit=1, fmt='(A)'), separator
        write(unit=1, fmt='(A,I0,A)'), 'mesh = Mesh(', mesh%m_space, ', nodeData, elementData)'
        write(unit=1, fmt='(A)'), ''
        
        ! nodal scalar fields
        write(unit=1, fmt='(A)'), separator
        write(unit=1, fmt='(A,A)'), comment, 'NODAL SCALAR FIELDS'
        write(unit=1, fmt='(A)'), separator
        write(unit=1, fmt='(A)'), 'names = ('
        do i = 1, odb%n_fields
            write(unit=1, fmt='(A,"''",A,"'',")'), indentation, trim(odb%names(i))
        end do
        write(unit=1, fmt='(A)'), ')'
        write(unit=1, fmt='(A)'), ''
        write(unit=1, fmt='(A)'), 'values = ('
        do i = 1, mesh%n_nodes
            write(unit=1, fmt='(A,"(")', advance='no'), indentation
            do j = 1, odb%n_fields
                write(unit=1, fmt='(SP,E15.8)', advance='no'), odb%values(i, j)
                if (j < odb%n_fields) then
                    write(unit=1, fmt='(A)', advance='no'), ', '
                else
                    write(unit=1, fmt='(A)', advance='yes'), '),'
                end if
            end do
        end do
        write(unit=1, fmt='(A)'), ')'
        write(unit=1, fmt='(A)'), ''
        
        ! output database
        write(unit=1, fmt='(A)'), separator
        write(unit=1, fmt='(A,A)'), comment, 'OUTPUT DATABASE'
        write(unit=1, fmt='(A)'), separator
        write(unit=1, fmt='(A)'), 'outputDatabase = OutputDatabase(mesh, names, values)'
        write(unit=1, fmt='(A)'), ''
        
        ! close file
        close(unit=1)
    end subroutine
    
end module
